#!/usr/bin/perl -w
# $Id: console_starter 1200 2019-08-04 16:17:20Z mueller $
# SPDX-License-Identifier: GPL-3.0-or-later
# Copyright 2009-2019 by Walter F.J. Mueller <W.F.J.Mueller@gsi.de>
#
#  Revision History:
# Date         Rev  Vers    Comment
# 2019-08-04  1200   1.2    change simh port handling
# 2019-07-13  1189   1.1.1  drop superfluous exists for $opts
# 2014-08-10   581   1.1    rename to console_starter
# 2010-07-04   312   1.0.3  correct telnet_wrapper path
# 2010-04-26   284   1.0.2  add error check for GetOptions
# 2009-11-08   248   1.0.1  proper -h handling & text; add -t support;
# 2009-11-07   247   1.0    Initial version
#

use 5.14.0;                                 # require Perl 5.14 or higher
use strict;                                 # require strict checking

use Getopt::Long;

my %opts = ();
GetOptions(\%opts, "h", "t:s", "d:s", "s", "w", "l")
  or die "bad options";

if ($opts{h}) {
  print "usage: console_starter [-h] [-t type] [-d type] [-s] [-w] [-l]\n";
  print "  -h       help, print this text and quit\n";
  print "  -t term  set terminal type, vt100 or vt52 (def: vt100)\n";
  print "  -d dev   set device type, DLx or DZx for x'the line (def: DL0)\n";
  print "  -s       use simh ports, default is to use rri ports\n";
  print "  -w       use wide 132 column screen (default 80 columns)\n";
  print "  -l       use long 48 lines screen (default 24 lines)\n";
  exit 0;
}

my $emu    = "xterm";
my $telnet = $ENV{"RETROBASE"} . "/tools/bin/telnet_wrapper";

my @args;
my $term = "vt100";
my $dev  = "DL";
my $line = 0;
my $port;
my $title;

if (exists $opts{t}) {
  if ($opts{t} =~ m{^(vt100|vt52)$} ) {
    $term = $opts{t};
  } else {
    printf "unsupported terminal type: %s\n", $opts{t};
    exit 1;
  }
}

if (exists $opts{d}) {
  if ($opts{d} =~ m{^(DL|DZ)(\d*)$}i ) {
    $dev  = uc $1;
    $line = int $2;
  } else {
    printf "unsupported device type: %s\n", $opts{d};
    exit 1;
  }
}

if ($opts{s}) {                             # simh ports
  $port = 5670+$line if ($dev eq "DL");       # telnet cons + 1 line DL mux
  $port = 5672       if ($dev eq "DZ");       # 8 line DZ mux
  $title = sprintf "\"%s%d %s (%4d)\"", $dev, $line, $term, $port;
} else {                                    # rri ports
  $port = 8000+$line if ($dev eq "DL");
  $port = 8002+$line if ($dev eq "DZ");
  $title = sprintf "\"%s%d %s (%4d)\"", $dev, $line, $term, $port;
}

my $geo_w = 80;
my $geo_l = 24;
$geo_w = 132 if $opts{w};
$geo_l =  48 if $opts{l};

push @args, "-j", "-rightbar", "-sb", "-sl", "500";
push @args, "-bg", "gray90", "-fg", "black";
push @args, "-ti", $term;
push @args, "-geo", sprintf("%dx%d", $geo_w, $geo_l);
push @args, "-T", $title;
push @args, "-e", $telnet, "localhost", sprintf("%d",$port);

print $emu, " ", join " ",@args, "\n";

my $rc = system $emu, @args;
if ($rc != 0) {
  print STDERR "xterm failed with rc=$rc\n";
}
